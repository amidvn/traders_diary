
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// регистр ОстаткиДенежныхСредств 
	Движения.ОстаткиДенежныхСредств.Записывать = Истина;
	Для Каждого ТекСтрока Из ИзмененияОстатковДенежныхСредств Цикл
		Движение = Движения.ОстаткиДенежныхСредств.Добавить();
		Движение.ВидДвижения = ОпределитьВидДвижения(ТекСтрока.ПричинаИзменения);
		Движение.Период = ТекСтрока.ДатаВремяСделки;
		Движение.БрокерскийСчет = БрокерскийСчет;
		ЗаполнитьЗначенияСвойств(Движение, ТекСтрока);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьВидДвижения(ПричинаИзменения)
	
	Если ПричинаИзменения = Перечисления.ПричиныИзмененияОстатковДенежныхСредств.ПополнениеБрокерскогоСчета
	ИЛИ ПричинаИзменения = Перечисления.ПричиныИзмененияОстатковДенежныхСредств.ВыплатаКупонов
	ИЛИ ПричинаИзменения = Перечисления.ПричиныИзмененияОстатковДенежныхСредств.ПогашениеОблигации
	ИЛИ ПричинаИзменения = Перечисления.ПричиныИзмененияОстатковДенежныхСредств.ОбменВалютыПоступление
	Тогда
		Возврат ВидДвиженияНакопления.Приход;
	Иначе
		Возврат ВидДвиженияНакопления.Расход;
	КонецЕсли;		
	
КонецФункции
	
#КонецОбласти

#КонецЕсли